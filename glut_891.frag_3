#version 130

// glut_891_common.glsl
#define PI 3.1415926535

#define SAT(x) clamp(x, 0., 1.)

#define TERRAIN_FREQ .1
#define TERRAIN_HEIGHT 3.
#define HQ_OCTAVES 12
#define MQ_OCTAVES 7

#define CAMERA_NEAR .001
#define CAMERA_FAR 200.
#define CAMERA_FOV 75.
#define CAMERA_HEIGHT 1.6
#define CAMERA_PITCH .15
#define CAMERA_ZOOM -2.
#define CAMERA_DEPTH -1125.

#define FOG_B .3
#define FOG_C .1

#define SUN_INTENSITY 6.66
#define SUN_COLOR vec3(1.2, 1., .6)
#define SKY_COLOR vec3(.25, .5, 1.75)
#define SUN_SPEED .04

#define EARTH_RADIUS 6378100. 
#define CLOUD_BOTTOM 3200.
#define CLOUD_TOP 4800.
#define CLOUD_COVERAGE .48
#define CLOUD_BASE_FREQ .00005
#define CLOUD_DETAIL_FREQ .0015
#define CLOUD_STEPS 18
#define CLOUD_LIGHT_STEPS 6
#define CLOUD_TOP_OFFSET 250.
#define CLOUD_ABSORPTION_TOP 1.8
#define CLOUD_ABSORPTION_BOTTOM 3.6

#define WIND_DIR vec3(.4, .1, 1.)
#define WIND_SPEED 75.

#define CLOUDS_AMBIENT_TOP vec3(1., 1.2, 1.6)
#define CLOUDS_AMBIENT_BOTTOM vec3(.6, .4, .8)

#define BAYER_LIMIT 16
#define BAYER_LIMIT_H 4

const int bayerFilter[BAYER_LIMIT] = int[]
(
   0,  8,  2, 10,
  12,  4, 14,  6,
   3, 11,  1,  9,
  15,  7, 13,  5
);

struct Ray
{
  vec3 origin, direction;   
};
    
float
remap (float x, float a, float b, float c, float d)
{
  return (((x - a) / (b - a)) * (d - c)) + c;
}

float
remap01 (float x, float a, float b)
{
  return ((x - a) / (b - a));   
}

bool
writeToPixel (vec2 fragCoord, int iFrame_)
{
  ivec2 iFragCoord = ivec2(fragCoord);
  int index = iFrame_ % BAYER_LIMIT;
  return (((iFragCoord.x + BAYER_LIMIT_H * iFragCoord.y) % BAYER_LIMIT) == bayerFilter[index]);
}

mat3
getCameraMatrix (vec3 origin, vec3 target)
{
  vec3 lookAt = normalize(target - origin);
  vec3 right = normalize(cross(lookAt, vec3(0., 1., 0.)));
  vec3 up = normalize(cross(right, lookAt));
  return mat3(right, up, -lookAt);
}

Ray
getCameraRay (vec2 uv, float t)
{
  uv *= (CAMERA_FOV / 360.) * PI;
  vec3 origin = vec3(0., CAMERA_HEIGHT, CAMERA_DEPTH);
  vec3 target = vec3(0., origin.y + CAMERA_PITCH,  CAMERA_DEPTH - 1.2);
  mat3 camera = getCameraMatrix(origin, target);
  vec3 direction = normalize(camera * vec3(uv, CAMERA_ZOOM));
  return Ray(origin, direction);
}

vec3
getSun (vec2 mouse, float iTime_)
{
  vec2 sunPos = mouse;
    
  if (mouse.y < -.95)
  {
    sunPos = vec2(cos(mod(iTime_ * SUN_SPEED, PI)) * .7, 0.);
    sunPos.y = 1. - 3.05 * sunPos.x * sunPos.x;
  }

  float sunHeight = (max(0., sunPos.y * .75 + .25));

  return vec3(sunPos, sunHeight);
}

vec3
miePhase (float dist, vec3 sunL)
{
  return max(exp(-pow(dist, .3)) * sunL - .4, 0.);
}

vec3
atmosphericScattering (vec2 uv, vec2 sunPos, bool isSun)
{
  float sunDistance = distance(uv, sunPos);
  float scatterMult = SAT(sunDistance);
  float dist = uv.y;
  dist = (.5 * mix(scatterMult, 1., dist)) / dist;
  vec3 mieScatter = miePhase(sunDistance, vec3(1.)) * SUN_COLOR;
  vec3 color = dist * SKY_COLOR;
  color = max(color, 0.);
  vec3 sun = .0002 / pow(length(uv-sunPos), 1.7) * SUN_COLOR;
    
  color = max(mix(pow(color, .8 - color),
  color / (2. * color + .5 - color * 1.3),
  SAT(sunPos.y * 2.5)), 0.)
  + (isSun ? (sun + mieScatter) : vec3(0.));

  color *=  (pow(1. - scatterMult, 5.) * 10. * SAT(.666 - sunPos.y)) + 1.5;
  float underscatter = distance(sunPos.y, 1.);
  color = mix(color, vec3(0.), SAT(underscatter));

  return color;	
}

#define UI0 1597334673U
#define UI1 3812015801U
#define UI2 uvec2(UI0, UI1)
#define UI3 uvec3(UI0, UI1, 2798796415U)
#define UIF (1. / float(0xffffffffU))

vec3
hash33 (vec3 p)
{
  uvec3 q = uvec3(ivec3(p)) * UI3;
  q = (q.x ^ q.y ^ q.z)*UI3;
  return -1. + 2. * vec3(q) * UIF;
}

float
hash13 (vec3 p)
{
  uvec3 q = uvec3(ivec3(p)) * UI3;
  q *= UI3;
  uint n = (q.x ^ q.y ^ q.z) * UI0;
  return float(n) * UIF;
}

float
hash12 (vec2 p)
{
  uvec2 q = uvec2(ivec2(p)) * UI2;
  uint n = (q.x ^ q.y) * UI0;
  return float(n) * UIF;
}

vec3
valueNoiseDerivative (vec2 x, sampler2D smp)
{
  vec2 f = fract(x);
  vec2 u = f * f * (3. - 2. * f);

#if 1
  ivec2 p = ivec2(floor(x));
  float a = texelFetch(smp, (p + ivec2(0, 0)) & 255, 0).x;
  float b = texelFetch(smp, (p + ivec2(1, 0)) & 255, 0).x;
  float c = texelFetch(smp, (p + ivec2(0, 1)) & 255, 0).x;
  float d = texelFetch(smp, (p + ivec2(1, 1)) & 255, 0).x;
#else    
  vec2 p = floor(x);
  float a = textureLod(smp, (p + vec2(.5, .5)) / 256., 0.).x;
  float b = textureLod(smp, (p + vec2(1.5, .5)) / 256., 0.).x;
  float c = textureLod(smp, (p + vec2(.5, 1.5)) / 256., 0.).x;
  float d = textureLod(smp, (p + vec2(1.5, 1.5)) / 256., 0.).x;
#endif
    
  return vec3(a + (b - a) * u.x + (c - a) * u.y + (a - b - c + d) * u.x * u.y,
              6. * f * (1. - f) * (vec2(b - a, c - a) + (a - b - c + d) * u.yx));
}

float
valueNoise (vec3 x, float freq)
{
  vec3 i = floor(x);
  vec3 f = fract(x);
  f = f * f * (3. - 2. * f);
  
  return mix(mix(mix(hash13(mod(i + vec3(0, 0, 0), freq)),  
                     hash13(mod(i + vec3(1, 0, 0), freq)), f.x),
                 mix(hash13(mod(i + vec3(0, 1, 0), freq)),  
                     hash13(mod(i + vec3(1, 1, 0), freq)), f.x), f.y),
             mix(mix(hash13(mod(i + vec3(0, 0, 1), freq)),  
                     hash13(mod(i + vec3(1, 0, 1), freq)), f.x),
                 mix(hash13(mod(i + vec3(0, 1, 1), freq)),  
                     hash13(mod(i + vec3(1, 1, 1), freq)), f.x), f.y), f.z);
}

float
worleyNoise (vec3 uv, float freq, bool tileable)
{    
  vec3 id = floor(uv);
  vec3 p = fract(uv);
  float minDist = 10000.;
    
  for (float x = -1.; x <= 1.; ++x)
    for(float y = -1.; y <= 1.; ++y)
      for(float z = -1.; z <= 1.; ++z)
      {
        vec3 offset = vec3(x, y, z);
        vec3 h = vec3(0.);
        if (tileable)
          h = hash33(mod(id + offset, vec3(freq))) * .4 + .3;
        else
          h = hash33(id + offset) * .4 + .3;
        h += offset;
        vec3 d = p - h;
        minDist = min(minDist, dot(d, d));
      }
    
  return 1. - minDist;
}

float
perlinFbm (vec3 p, float freq, int octaves)
{
  float G = exp2(-.85);
  float amp = 1.;
  float noise = 0.;
  for (int i = 0; i < octaves; ++i)
  {
    noise += amp * valueNoise(p * freq, freq);
    freq *= 2.;
    amp *= G;
  }
    
  return noise;
}

float
worleyFbm (vec3 p, float freq, bool tileable)
{
  float fbm = worleyNoise(p * freq, freq, tileable) * .625 +
              worleyNoise(p * freq * 2., freq * 2., tileable) * .25 +
              worleyNoise(p * freq * 4., freq * 4., tileable) * .125;
  return max(0., fbm * 1.1 - .1);
}
// glut_891_common.glsl

uniform vec2 iResolution;
uniform float iTime;
uniform int iFrame;
uniform vec4 iMouse;
uniform sampler2D iChannel0;
uniform sampler2D iChannel1;
uniform sampler2D iChannel2;
uniform sampler2D iChannel3;

const vec3 noiseKernel[6u] = vec3[] 
(
  vec3( .38051305,  .92453449, -.02111345),
  vec3(-.50625799, -.03590792, -.86163418),
  vec3(-.32509218, -.94557439,  .01428793),
  vec3( .09026238, -.27376545,  .95755165),
  vec3( .28128598,  .42443639, -.86065785),
  vec3(-.16852403,  .14748697,  .97460106)
);

float
raySphereIntersect (Ray ray, float radius)
{
  float b = 2. * dot(ray.origin, ray.direction);
  float c = dot(ray.origin, ray.origin) - radius * radius;
  float d = sqrt(b * b - 4. * c);
  return (-b + d) * .5;
}

float
cloudGradient (float h)
{
  return smoothstep(0., .05, h) * smoothstep(1.25, .5, h);
}

float
cloudHeightFract (float p)
{
  return (p - EARTH_RADIUS - CLOUD_BOTTOM) / (CLOUD_TOP - CLOUD_BOTTOM);
}

float
cloudBase (vec3 p, float y)
{
  vec3 noise = textureLod(iChannel2, (p.xz - (WIND_DIR.xz * iTime * WIND_SPEED)) * CLOUD_BASE_FREQ, 0.).rgb;
  float n = y * y * noise.b + pow(1. - y, 12.);
  float cloud = remap01(noise.r - n, noise.g - 1., 1.);
  return cloud;
}

float
cloudDetail (vec3 p, float c, float y)
{
  p -= WIND_DIR * 3. * iTime * WIND_SPEED;
  float hf = worleyFbm(p, CLOUD_DETAIL_FREQ, false) * .625 +
             worleyFbm(p, CLOUD_DETAIL_FREQ*2., false) * .25 +
             worleyFbm(p, CLOUD_DETAIL_FREQ*4., false) * .125;
  hf = mix(hf, 1. - hf, y * 4.);
  return remap01(c, hf * .5, 1.);
}

float
getCloudDensity (vec3 p, float y, bool detail)
{
  p.xz -= WIND_DIR.xz * y * CLOUD_TOP_OFFSET;
  float d = cloudBase(p, y);
  d = remap01(d, CLOUD_COVERAGE, 1.) * (CLOUD_COVERAGE);
  d *= cloudGradient(y);
  bool cloudDetailTest = (d > 0. && d < .3) && detail; 
  return ((cloudDetailTest) ? cloudDetail(p, d, y) : d);
}

float
henyeyGreenstein (float sunDot, float g)
{
  float g2 = g * g;
  return (.25 / PI) * ((1. - g2) / pow( 1. + g2 - 2. * g * sunDot, 1.5));
}

float
marchToLight (vec3 p, vec3 sunDir, float sunDot, float scatterHeight)
{
  float lightRayStepSize = 11.;
  vec3 lightRayDir = sunDir * lightRayStepSize;
  vec3 lightRayDist = lightRayDir * .5;
  float coneSpread = length(lightRayDir);
  float totalDensity = 0.;
  for(int i = 0; i < CLOUD_LIGHT_STEPS; ++i)
  {
    vec3 cp = p + lightRayDist + coneSpread * noiseKernel[i] * float(i);
    float y = cloudHeightFract(length(p));
    if (y > .95 || totalDensity > .95) break;
    totalDensity += getCloudDensity(cp, y, false) * lightRayStepSize;
    lightRayDist += lightRayDir;
  }

  return 32. * exp(-totalDensity * mix(CLOUD_ABSORPTION_BOTTOM, CLOUD_ABSORPTION_TOP, scatterHeight)) * (1. - exp(-totalDensity * 2.));
}

void
main ()
{
  vec2 st = gl_FragCoord.xy / iResolution;
  vec2 uv = (2. * gl_FragCoord.xy - iResolution) / iResolution.y;
  vec2 mouse = (2. * iMouse.xy - iResolution) / iResolution.y;
  float terrainDist = texelFetch(iChannel0, ivec2(gl_FragCoord.xy), 0).w;
  vec4 prevCol = textureLod(iChannel1, st, 0.);
  vec4 col = vec4(0.);

  bool updatePixel = writeToPixel(gl_FragCoord.xy, iFrame);
    
  if (updatePixel)
  {
    Ray ray = getCameraRay(uv, iTime);
    vec3 sun = getSun(mouse, iTime);
    sun.z = clamp(sun.z, 0., .8);
    vec3 sunDir = normalize(vec3(sun.x, sun.z, -1.));
    float sunDot = max(0., dot(ray.direction, sunDir));
    float sunHeight = smoothstep(.01, .1, sun.z + .025);

    if (terrainDist > CAMERA_FAR)
    {
      ray.origin.y = EARTH_RADIUS;
      float start = raySphereIntersect(ray, EARTH_RADIUS + CLOUD_BOTTOM);
      float end = raySphereIntersect(ray, EARTH_RADIUS + CLOUD_TOP);
      float cameraRayDist = start;
      float cameraRayStepSize = (end - start) / float(CLOUD_STEPS);
            
      cameraRayDist += cameraRayStepSize * texelFetch(iChannel3, (ivec2(gl_FragCoord.xy) + iFrame * ivec2(113, 127)) & 1023, 0).r;
      vec3 skyCol = atmosphericScattering(vec2(0.15, 0.05), vec2(.5, sun.y*.5+.25), false);
      skyCol.r *= 1.1;
      skyCol = SAT(pow(skyCol * 2.1, vec3(4.2)));
      float sunScatterHeight = smoothstep(.15, .4, sun.z);
      float hgPhase = mix(henyeyGreenstein(sunDot, .4),
                          henyeyGreenstein(sunDot, -.1), .5);
      hgPhase = max(hgPhase, 1.6 * henyeyGreenstein(sqrt(sunDot), SAT(.8 - sunScatterHeight)));
      hgPhase = mix(pow(sunDot, .25), hgPhase, sunHeight);

      vec4 intScatterTrans = vec4(0., 0., 0., 1.);
      vec3 ambient = vec3(0.);
      for (int i = 0; i < CLOUD_STEPS; ++i)
      {
        vec3 p = ray.origin + cameraRayDist * ray.direction;
        float heightFract = cloudHeightFract(length(p));
        float density = getCloudDensity(p, heightFract, true);
        if (density > 0.)
        {
          ambient = mix(CLOUDS_AMBIENT_BOTTOM, CLOUDS_AMBIENT_TOP, 
                          heightFract);
          
          vec3 luminance = (ambient * SAT(pow(sun.z + .04, 1.4)) + skyCol * .125 + (sunHeight * skyCol + vec3(.0075, .015, .03)) * SUN_COLOR * hgPhase * marchToLight(p, sunDir, sunDot, sunScatterHeight)) * density;
          float transmittance = exp(-density * cameraRayStepSize);
          vec3 integScatter = (luminance - luminance * transmittance) * (1. / density);
          intScatterTrans.rgb += intScatterTrans.a * integScatter; 
          intScatterTrans.a *= transmittance;
        }

        if (intScatterTrans.a < .05)
          break;
        cameraRayDist += cameraRayStepSize;
      }

      float fogMask = 1. - exp(-smoothstep(.15, 0., ray.direction.y) * 2.);
      vec3 fogCol = atmosphericScattering(uv * .5 + .2, sun.xy * .5 + .2, false);
      intScatterTrans.rgb = mix(intScatterTrans.rgb, fogCol * sunHeight, fogMask);
      intScatterTrans.a = mix(intScatterTrans.a, 0., fogMask);
      col = vec4(max(vec3(intScatterTrans.rgb), 0.), intScatterTrans.a);
            
      col = mix(prevCol, col, .5);
    }
  }
  else
    col = prevCol;
    
  gl_FragColor = col;
}
