#version 330
//precision mediump float;

//#include "glut_484_common.glsl"
#define ch0 iChannel0
#define ch1 iChannel1
#define ch2 iChannel2
#define ch3 iChannel3

#define R iResolution.xy

#define LOAD(ch, pos) texelFetch(ch, ivec2(pos), 0)
#define LOAD3D(ch, pos) texelFetch(ch, ivec2(dim2from3(pos)), 0)

#define PI 3.1415926535
#define TWO_PI 6.28318530718

#define light_dir normalize(vec3(0.741,1.000,0.580))

#define initial_particle_density 1u
#define dt 1.0
#define rest_density 0.5
#define gravity 0.01

#define PRESSURE 3.5
#define PRESSURE_RAD 1.0
#define VISCOSITY 0.65
#define SURFACE_TENSION 0.1
#define SURFACE_TENSION_RAD 2.0
#define SPIKE_KERNEL 0.5
#define SPIKE_RAD 0.75

#define force_mouse 0.005
#define force_mouse_rad 40.0
#define force_boundary 5.0
#define boundary_h 5.0
#define max_velocity 1.0
#define cooling 0.0

float sqr(float x)
{
    return x * x;
}

float cub(float x)
{
    return x*x*x;
}

float Pressure(float rho)
{
    return (rho/rest_density - 1.0)/max(rho*rho, 0.001);
}

float Gaussian(float r, float d)
{
    float norm = 1.0/(cub(d)*sqrt(cub(TWO_PI)));
    return norm * exp(-0.5*sqr(r/d));
}

vec3 GaussianGrad(vec3 dx, float d)
{
    float norm = 1.0/(cub(d)*sqrt(cub(TWO_PI)));
    float r = length(dx);
    return - (norm/sqr(d)) * exp(-0.5*sqr(r/d)) * dx;
}

vec4 GaussianGrad2(vec3 dx, float d)
{
    float norm = 1.0/(cub(d)*sqrt(cub(TWO_PI)));
    float r = length(dx);
    return norm * exp(-0.5*sqr(r/d)) * vec4(-dx /sqr(d), 1.0);
}

#define KERNEL_POLY6_NORMALIZATION (315.0 / (64.0 * PI))
#define KERNEL_SPIKE_NORMALIZATION (45 / PI)
#define KERNEL_RADIUS 1.0

float KernelPoly6(float r, float d)
{
    float c = KERNEL_POLY6_NORMALIZATION / cub(cub(d));
    return (r <= d) ? c * cub(sqr(d) - sqr(r)) : 0.0;
}

vec3 KernelPoly6Grad(vec3 dx, float d)
{
    float c = KERNEL_POLY6_NORMALIZATION / cub(cub(d));
    float r = length(dx);
    return (r <= d) ? clamp(-6.0 * c * sqr(sqr(d) - sqr(r)), -3.0, 0.0) * dx : vec3(0.0);
}

#define GD(x, R) Gaussian(length(x),R)
#define GGRAD(x, R) GaussianGrad(x, R)
#define GGRAD2(x,R) GaussianGrad2(x, R)
//#define GD(x, R) 12.0*KernelPoly6(length(x),R*4.0/2.0)
//#define GGRAD(x, R) 12.0*KernelPoly6Grad(x, R*4.0/2.0)
#define GS(x) exp(-dot(x,x))
#define DIR(phi) vec2(cos(TWO_PI*phi),sin(TWO_PI*phi))

#define loop(i,x) for(int i = 0; i < x; i++)
#define range(i,a,b) for(int i = a; i <= b; i++)

//3d slice aspect ratio 
#define ar vec2(1.0,0.5)
vec2 SCALE;
vec3 size3d;

vec2 dim2from3(vec3 p3d)
{
    p3d = clamp(p3d, vec3(0.0), size3d);
    float ny = floor(p3d.z/SCALE.x);
    float nx = floor(p3d.z) - ny*SCALE.x;
    return vec2(nx, ny)*vec2(size3d.xy) + p3d.xy;
}

vec3 dim3from2(vec2 p2d)
{
    return vec3(p2d - size3d.xy*floor(p2d/size3d.xy),(floor(p2d.x/size3d.x) + SCALE.x*floor(p2d.y/size3d.y)));
}


//#define pixel(a, p, s) texture(a, (p+0.5)/vec2(s))

vec4 pixel(sampler2D ch, vec2 pos)
{
    vec2 d = pos - floor(pos);
    vec4 p00 = texelFetch(ch, ivec2(pos), 0);
    vec4 p01 = texelFetch(ch, ivec2(pos) + ivec2(0, 1), 0);
    vec4 p10 = texelFetch(ch, ivec2(pos) + ivec2(1, 0), 0);
    vec4 p11 = texelFetch(ch, ivec2(pos) + ivec2(1, 1), 0);
    return mix(mix(p00, p01, d.y), mix(p10, p11, d.y), d.x);
}

vec4 voxel(sampler2D ch, vec3 p3d)
{
    return pixel(ch, dim2from3(p3d));
}

//trilinear interpolation = linear interp between layers
vec4 trilinear(sampler2D ch, vec3 p3d)
{
    return mix(voxel(ch, vec3(p3d.xy, floor(p3d.z))),voxel(ch, vec3(p3d.xy, ceil(p3d.z))), fract(p3d.z));
}


float sdBox( vec3 p, vec3 b )
{
    vec3 d = abs(p) - b;
    return min(max(d.x,max(d.y,d.z)),0.0) + length(max(d,0.0));
}

vec2 hash21(float p)
{
  vec3 p3 = fract(vec3(p) * vec3(.1031, .1030, .0973));
  p3 += dot(p3, p3.yzx + 33.33);
    return fract((p3.xx+p3.yz)*p3.zy);
}

vec2 hash23(vec3 p3)
{
  p3 = fract(p3 * vec3(.1031, .1030, .0973));
    p3 += dot(p3, p3.yzx+33.33);
    return fract((p3.xx+p3.yz)*p3.zy);
}

vec3 udir(vec2 rng)
{
    float phi = 2.*PI*rng.x;
    float ctheta = 2.*rng.y-1.;
    float stheta = sqrt(1.0-ctheta*ctheta);
    return vec3(cos(phi)*stheta, sin(phi)*stheta, ctheta);
}

struct Particle 
{
    uint mass;
    vec3 pos;
    vec3 vel;
    vec3 force;
    float density;
};

//5 bits for shared exponent, 9 bits for each component
uint packvec3(vec3 v)
{
    //get the exponent
    float maxv = max(abs(v.x), max(abs(v.y), abs(v.z)));
    int exp = clamp(int(ceil(log2(maxv))), -15, 15);
    float scale = exp2(-float(exp));
    uvec3 sv = uvec3(round(clamp(v*scale, -1.0, 1.0) * 255.0) + 255.0);
    uint packed = uint(exp + 15) | (sv.x << 5) | (sv.y << 14) | (sv.z << 23);
    return packed;
}

vec3 unpackvec3(uint packed)
{
    int exp = int(packed & 0x1Fu) - 15;
    vec3 sv = vec3((packed >> 5) & 0x1FFu, (packed >> 14) & 0x1FFu, (packed >> 23) & 0x1FFu);
    vec3 v = (sv - 255.0) / 255.0;
    v *= exp2(float(exp));
    return v;
}

uint packmasspos(uint mass, vec3 p0)
{
    uvec3 pos0 = uvec3(clamp(p0, 0.0, 1.0) * 255.0);
    return mass | (pos0.x << 8) | (pos0.y << 16) | (pos0.z << 24);
}

uint packMassPos(uint mass, vec3 pos)
{
    uvec3 pos0 = uvec3(clamp(pos, 0.0, 1.0) * 255.0); // Assuming pos range [0, 1] in a cell
    uint data1 = mass | (pos0.x << 8) | (pos0.y << 16) | (pos0.z << 24);
    return data1;
}

void unpackMassPos(uint packed, out uint mass, out vec3 pos)
{
    mass = packed & 0xFFu;
    uvec3 pos0 = uvec3((packed >> 8) & 0xFFu, (packed >> 16) & 0xFFu, (packed >> 24) & 0xFFu);
    pos = vec3(pos0) / 255.0;
}

vec4 packParticles(Particle p0, Particle p1, vec3 pos)
{
    p0.pos -= pos;
    p1.pos -= pos;

    uvec4 data = uvec4(
        packMassPos(p0.mass, p0.pos),
        packMassPos(p1.mass, p1.pos),
        packvec3(p0.vel),
        packvec3(p1.vel)
    );

    return uintBitsToFloat(data);
}

void unpackParticles(vec4 packed, vec3 pos, out Particle p0, out Particle p1)
{
    uvec4 data = floatBitsToUint(packed);
    
    unpackMassPos(data.x, p0.mass, p0.pos);
    unpackMassPos(data.y, p1.mass, p1.pos);

    p0.pos += pos;
    p1.pos += pos;

    p0.vel = unpackvec3(data.z);
    p1.vel = unpackvec3(data.w);
}

float sdBox( in vec2 p, in vec2 b )
{
    vec2 d = abs(p)-b;
    return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
}

int ClosestCluster(Particle p0, Particle p1, Particle incoming)
{
    //first try to choose the particle with significantly smaller mass
    if(float(p0.mass) < 0.01*float(p1.mass) || float(p1.mass) < 0.01*float(p0.mass))
    {
        return p0.mass < p1.mass ? 0 : 1;
    }

    //otherwise choose the closest one
    float d0 = length(p0.pos - incoming.pos);
    float d1 = length(p1.pos - incoming.pos);
    return d0 < d1 ? 0 : 1;
}

void BlendParticle(inout Particle p, in Particle incoming)
{
    uint newMass = p.mass + incoming.mass;
    vec2 weight = vec2(p.mass, incoming.mass) / float(newMass);
    p.pos = p.pos*weight.x + incoming.pos*weight.y;
    p.vel = p.vel*weight.x + incoming.vel*weight.y;
    p.mass = newMass;
}

void Clusterize(inout Particle p0, inout Particle p1, in Particle incoming, vec3 pos)
{
    //check if the incoming particle is in the cell
    if(!all(equal(pos, floor(incoming.pos))))
    {
        return;
    }

    int closest = ClosestCluster(p0, p1, incoming);
    if(closest == 0)
    {
        BlendParticle(p0, incoming);
    }
    else
    {
        BlendParticle(p1, incoming);
    }
}

void SplitParticle(inout Particle p1, inout Particle p2)
{
    vec3 pos = p1.pos;
    uint newMass = p1.mass;
    p1.mass = newMass/2u;
    p2.mass = newMass - p1.mass;
    vec3 dir = udir(hash23(pos));
    p1.pos = pos - dir*5e-3;
    p2.pos = pos + dir*5e-3;
    p2.vel = p1.vel;
}

void ApplyForce(inout Particle p, in Particle incoming)
{
    if(incoming.mass == 0u) return;

    vec3 dx = incoming.pos - p.pos;
    vec3 dv = incoming.vel - p.vel;
    float d = length(dx);
    vec3 dir = dx / max(d, 1e-3);
    
    float rho0 = p.density;
    float rho1 = incoming.density;
    float mass0 = float(p.mass);
    float mass1 = float(incoming.mass);
  
    vec3 ggrad = GGRAD(dx, PRESSURE_RAD);
    float pressure = 0.5*p.density*(Pressure(p.density) + Pressure(incoming.density));
    vec3 F_SPH = - PRESSURE * pressure * ggrad;
    vec3 F_VISC = VISCOSITY * dot(dir, dv) * ggrad;
    vec3 F_ST = SURFACE_TENSION * GGRAD(dx, SURFACE_TENSION_RAD);
    vec3 F_SPIKE = SPIKE_KERNEL * GD(d, SPIKE_RAD) * dir;
   
    p.force += - (F_ST + F_SPH + F_VISC + F_SPIKE) * mass1;
    
}

float minv(vec3 a)
{
    return min(min(a.x, a.y),a.z);
}

float maxv(vec3 a)
{
    return max(max(a.x, a.y),a.z);
}

float distance2border(vec3 p)
{
    vec3 a = vec3(size3d - 1.) - p;
    return min(minv(p),minv(a)) + 1.;
}

vec4 border_grad(vec3 p)
{
    const float dx = 0.001;
    const vec3 k = vec3(1,-1,0);
  return  (k.xyyx*distance2border(p + k.xyy*dx) +
       k.yyxx*distance2border(p + k.yyx*dx) +
       k.yxyx*distance2border(p + k.yxy*dx) +
       k.xxxx*distance2border(p + k.xxx*dx))/vec4(4.*dx,4.*dx,4.*dx,4.);
}

void IntegrateParticle(inout Particle p, vec3 pos, vec2 iR, vec4 iM, float time)
{
    p.force = p.force;/// max(0.0001, float(p.mass));
    p.force += gravity*vec3(0.4*sin(1.5*time), 0.2*cos(0.75*time), -1.0); //gravity
    

    vec4 border = border_grad(p.pos);
    vec3 bound =1.*normalize(border.xyz)*exp(-0.4*border.w*border.w);
    p.force += force_boundary*bound*dt;
    //p.force += vec2(0.0, 0.0)*GS(distance(p.pos, iR*vec2(0.2,0.5))/force_mouse_rad);
 
    //if(iM.z > 0.)
    //{
    //    vec3 dx = pos - vec3(iM.xy, 0.0);
    //    p.force -= force_mouse*dx*GS(dx/force_mouse_rad);
    //}
        
    p.vel += p.force * dt;

    //velocity limit
    float v = length(p.vel)/max_velocity;
    p.vel /= (v > 1.)?v:1.;
}

void InitGrid(vec2 iR)
{
    SCALE = floor(ar*pow(iR.x*iR.y,0.1666666));
    size3d = vec3(floor(iR.xy/SCALE), SCALE.x*SCALE.y);
}

vec3 hsv2rgb( in vec3 c )
{
    vec3 rgb = clamp( abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),6.0)-3.0)-1.0, 0.0, 1.0 );

  rgb = rgb*rgb*(3.0-2.0*rgb); // cubic smoothing	

  return c.z * mix( vec3(1.0), rgb, c.y);
}
//#include "glut_484_common.glsl"

uniform vec2 iResolution;
uniform vec4 iMouse;
uniform sampler2D iChannel0;
uniform sampler2D iChannel1;
//uniform sampler2D iChannel2;
uniform samplerCube iChannel3;

#define SHADOWS
#define REFRACTION

#define RADIUS 0.65
#define NORMAL_SMOOTHNESS 1.0

#define FOV 2.5
mat3 getCamera(vec2 angles)
{
   mat3 theta_rot = mat3(1,   0,              0,
                          0,  cos(angles.y),  -sin(angles.y),
                          0,  sin(angles.y),  cos(angles.y)); 
        
   mat3 phi_rot = mat3(cos(angles.x),   sin(angles.x), 0.,
                   -sin(angles.x),   cos(angles.x), 0.,
                    0.,              0.,            1.); 
        
   return theta_rot*phi_rot;
}

vec3 getRay(vec2 angles, vec2 pos)
{
    mat3 camera = getCamera(angles);
    return normalize(transpose(camera)*vec3(FOV*pos.x, 1., FOV*pos.y));
}


#define MAX_DIST 1e10

struct Ray 
{
    vec3 ro;
    vec3 rd;
    float td;
    vec3 normal;
    vec3 color;
};

void iSphere(inout Ray ray, vec4 sphere, vec3 color)
{
    vec3 ro = ray.ro - sphere.xyz;
    float b = dot(ro, ray.rd);
    float c = dot(ro, ro) - sphere.w*sphere.w;
    float h = b*b - c;
    if (h > 0.) 
    {
      h = sqrt(h);
        float d1 = -b-h;
        float d2 = -b+h;
        if (d1 >= 0.0 && d1 <= ray.td) {
            ray.normal = normalize(ro + ray.rd*d1);
            ray.color = color;
            ray.td = d1;
        } else if (d2 >= 0.0 && d2 <= ray.td) { 
            ray.normal = normalize(ro + ray.rd*d2); 
            ray.color = color;
            ray.td = d2;
        }
    }
}

vec2 iBox( in vec3 ro, in vec3 rd, in vec3 boxSize ) 
{
    vec3 m = sign(rd)/max(abs(rd), 1e-8);
    vec3 n = m*ro;
    vec3 k = abs(m)*boxSize;
  
    vec3 t1 = -n - k;
    vec3 t2 = -n + k;

  float tN = max( max( t1.x, t1.y ), t1.z );
  float tF = MAX_DIST;
    if(rd.x != 0.0) tF = min(tF, t2.x);
    if(rd.y != 0.0) tF = min(tF, t2.y);
    if(rd.z != 0.0) tF = min(tF, t2.z);
  
    if (tN > tF || tF <= 0.) {
        return vec2(MAX_DIST);
    } else {
        return vec2(tN, tF);
    }
}


void TraceCell(inout Ray ray, vec3 p)
{
    //load the particles 
    vec4 packed = LOAD3D(ch0, p);
    Particle p0, p1;
    unpackParticles(packed, p, p0, p1);

    if(p0.mass > 0u) iSphere(ray, vec4(p0.pos, RADIUS*1.5), vec3(1.000,1.000,1.000));
    if(p1.mass > 0u) iSphere(ray, vec4(p1.pos, RADIUS*1.5), vec3(1.000,1.000,1.000));
}

void TraceCells(inout Ray ray, vec3 p)
{
    vec3 p0 = floor(p);
    vec4 rho = voxel(ch1, p);
    if(rho.z < 1e-3) return;
    range(i, -1, 1) range(j, -1, 1) range(k, -1, 1)
    {
        //load the particles 
        vec3 p1 = p0 + vec3(i, j, k);
        TraceCell(ray, p1);
    }
}


float Density(vec3 p)
{
    return trilinear(ch1, p).z;
}

/* float Shadow(vec3 p)
{
    float optical_density = trilinear(ch2, p).x;
    return exp(-optical_density)+0.05;
} */

vec4 calcNormal(vec3 p, float dx) {
  const vec3 k = vec3(1,-1,0);
  return   (k.xyyx*Density(p + k.xyy*dx) +
       k.yyxx*Density(p + k.yyx*dx) +
       k.yxyx*Density(p + k.yxy*dx) +
       k.xxxx*Density(p + k.xxx*dx))/vec4(4.*dx,4.*dx,4.*dx,4.);
}

float TraceDensity(vec3 ro, vec3 rd)
{
    float dens = 0.0;
    float td = 0.0;
    for(int i = 0; i < 100; i++)
    {
        vec3 p = ro + rd * td;
        if(any(lessThan(p, vec3(1.0))) || any(greaterThan(p, size3d - 1.0))) return dens;
        float d = Density(p);
        dens += d * 2.0;
        td += 2.0;
    }
    return dens;
}


#define ISO_VALUE 0.5
float ParticleDensity(vec3 p)
{
    vec3 p0 = floor(p);
    float rho = voxel(ch1, p).z;
    //if(rho < 0.001) return rho;
    //rho = 0.0;
    ////if larger then compute accurate density from particles
    //range(i, -1, 1) range(j, -1, 1) range(k, -1, 1)
    //{
    //    //load the particles 
    //    vec3 p1 = p0 + vec3(i, j, k);
    //    //load the particles
    //    vec4 packed = LOAD3D(ch0, p1);
    //    Particle p0_, p1_;
    //    unpackParticles(packed, p1, p0_, p1_);
    //    if(p0_.mass > 0u) rho += float(p0_.mass)*GD(length(p0_.pos - p), RADIUS);
    //    if(p1_.mass > 0u) rho += float(p1_.mass)*GD(length(p1_.pos - p), RADIUS);
    //}

    return rho;
}

vec3 ParticleGradient(vec3 p)
{
    vec3 p0 = floor(p);
    vec3 grad = vec3(0.0);

    range(i, -1, 1) range(j, -1, 1) range(k, -1, 1)
    {
        //load the particles 
        vec3 p1 = p0 + vec3(i, j, k);

        //load the particles
        vec4 packed = LOAD3D(ch0, p1);
        Particle p0_, p1_;

        unpackParticles(packed, p1, p0_, p1_);

        if(p0_.mass > 0u) grad += float(p0_.mass)*GGRAD(p0_.pos - p, RADIUS);
        if(p1_.mass > 0u) grad += float(p1_.mass)*GGRAD(p1_.pos - p, RADIUS);
    }

    return grad;
}


float DE(vec3 p)
{
    return ISO_VALUE - ParticleDensity(p);
}

float TraceIsoSurface(Ray ray, float mint, float inside)
{
    const int step_count = 300;
    float td = mint;
    for(int i = 0; i < step_count; i++)
    {
        vec3 p = ray.ro + ray.rd * td;
        if(!all(lessThanEqual(p, size3d)) || !all(greaterThanEqual(p, vec3(0.0))))
        {
            return ray.td;
        }
        float d = inside*4.0*DE(p);
        if(d < 0.0)
        {
            return td;
        }
        td += d;
    }
    return td;
}

vec3 Background(vec3 rd)
{
    return 2.0*pow(texture(iChannel3,  rd.yzx).xyz, vec3(2.0));
}

void
main ()
{
    InitGrid(iResolution.xy);

    vec2 uv = (gl_FragCoord.xy - 0.5*R)/max(R.x, R.y);

    vec2 angles = vec2(2.*PI, PI)*(iMouse.xy/iResolution.xy - 0.5);

    if(iMouse.z <= 0.)
    {
        angles = vec2(0.35, -0.5);
    }
    vec3 rd = getRay(angles, uv);
    vec3 center_rd = getRay(angles, vec2(0.));
 
    float d = sqrt(dot(vec3(size3d), vec3(size3d)))*0.5;
    vec3 ro = vec3(size3d)*vec3(0.5, 0.5, 0.5) - center_rd*d;
    
    

    vec2 tdBox = iBox(ro - vec3(size3d)*0.5, rd, 0.5*vec3(size3d));
    gl_FragColor.rgb =Background (rd);
    if(tdBox.x < MAX_DIST)
    {
        float td = max(tdBox.x+0.5, 0.0);
        Ray ray;
        ray.ro = ro;
        ray.rd = rd;
        ray.td = tdBox.y;
        int i = 0;
        for(; i < 200; i++)
        {
            vec3 p = ro + rd*td;
            TraceCells(ray, p);
            td += 2.5;
            if(td > tdBox.y-1.0)
            {
                break;
            }
            if(ray.td < tdBox.y-1.0)
            {
                break;
            }
        }
        
        //col.xyz = vec3(i)/200.0;
        //return;
        
        //float liq_td = TraceIsoSurface(ray, td);
        //ray.td = min(liq_td, ray.td);
        
        if(ray.td < tdBox.y)
        {
            vec3 p0 = ray.ro + ray.rd*ray.td;
            vec3 normal = normalize(-ParticleGradient(p0));
            vec3 normal1 = normalize(calcNormal(p0, 0.5).xyz);
            normal = -normalize(mix(normal, normal1, NORMAL_SMOOTHNESS));
            vec3 albedo = vec3(0.039,0.153,1.000);
            float LdotN = 0.5*dot(normal, light_dir)+0.5;
#ifdef SHADOWS
                float shadow_d = TraceDensity(p0+light_dir*1.0, light_dir);
                float shadow = exp(-shadow_d) + 0.3*exp(-0.1*shadow_d);
#else
                float shadow = 1.0;
#endif
            vec3 refl_d = reflect(ray.rd, normal);
            vec3 refl = Background(refl_d);
            float K = 1. - pow(max(dot(normal,refl_d),0.), 2.);
            K = mix(0.0, K, 0.1);
            
#ifdef REFRACTION
            vec3 refr_d = refract(ray.rd, normal, 1.0/1.33);
            
            float liquid_density = TraceDensity(p0 + refr_d*1.0, refr_d);
            
            vec3 liquid_color = exp(-0.5*liquid_density*(1.0 - vec3(0.078,0.816,1.000)));
            vec3 refr_color = Background(refr_d) * liquid_color;
            gl_FragColor.rgb = 2.5*(0.5*shadow+0.5)*refr_color*(1.0 - K) + 0.*ray.color + 0.75*shadow*refl*K;
#else
            gl_FragColor.rgb = 2.5*shadow*albedo*LdotN*(1.0 - K) + 0.*ray.color + 0.75*shadow*refl*K;
#endif
        }
        
        //gl_FragColor.rgb = 0.01*vec3(1,1,1)*TraceDensity(ro + rd*max(tdBox.x+0.001,0.0), rd);
    }
    
    gl_FragColor.rgb = 1.0 - exp(-2.5*pow(gl_FragColor.rgb,vec3(1.0/1.4)));
}
