#version 130

// glut_717_common.glsl
// #define USE_CINEMATIC_MODE
// #define USE_BUMPY_STREAMS_MODE
// #define USE_GENERATION_SEED 123

const int nParticles = 20;
const float particlesSize = 8.0;
const float collisionDamping = 0.5;
const float streamsFadingExp = 0.001;
const float gravityStrength = 1.6 / particlesSize;

const vec3 ambientLightDir = normalize(vec3(1.0, 2.0, 0.0));
const vec3 ambientLightCol = vec3(1.1, 1.0, 0.9);
const vec3 backgroundColor = vec3(0.65);
const float streamsGlossExp = 120.0;
const float spotlightsGlare = 0.0;

#ifdef USE_BUMPY_STREAMS_MODE
#define particlesSize mix(particlesSize, particlesSize * 0.5, (1.0 + sin(1.85 + iTime * 11.93805208)) * 0.5)
#endif

#ifdef USE_GENERATION_SEED
#define generationSeed float(USE_GENERATION_SEED) // a fixed seed will generate the same output (in respect of the viewport size)
#else
#define generationSeed iDate.w // if no custom seed is provided, POSIX time is used instead (producing different results every time)
#endif

const ivec2 cameraVelocity =
#ifdef USE_CINEMATIC_MODE
ivec2(1, 0);
#else
ivec2(0);
#endif
// glut_717_common.glsl

uniform sampler2D iChannel0;
uniform sampler2D iChannel1;

vec2
getParticlePosition (int particleID)
{
  int iChannel0_width = textureSize (iChannel0, 0).x;
  ivec2 particleCoord = ivec2(particleID % iChannel0_width, particleID / iChannel0_width);

  return texelFetch(iChannel0, particleCoord, 0).xy;
}

void
main ()
{
  gl_FragColor = texelFetch(iChannel1, ivec2(gl_FragCoord.xy) + cameraVelocity, 0);

  for(int i = 0; i < nParticles; ++i)
  {
    vec2 v = gl_FragCoord.xy - getParticlePosition(i);

    float l = length(v);
    float alpha = smoothstep(particlesSize, particlesSize * 0.5, l);

    float z = sqrt(abs(particlesSize * particlesSize - l * l));
    vec3 n = normalize(vec3(v, z));

    gl_FragColor = mix(gl_FragColor, vec4(n, 1.0), alpha);
  }
}
