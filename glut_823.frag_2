#version 140

// glut_823_common.glsl
#define texel(a, p) texelFetch(a, ivec2(p), 0)
#define ch0 iChannel0
#define ch1 iChannel1
#define ch2 iChannel2
#define ch3 iChannel3
#define R iResolution.xy
#define PI 3.14159265

#define dt 1.
#define loop(i,x) for(int i = min(0, iFrame); i < x; i++)

#define SC 3.

#define smoothR 2.5
#define density 0.05

struct obj
{
  int id;
  vec3 X;
  float Rho;
  vec3 V;
  float Pressure;
  vec3 Color;
  float Scale;
};

float
Force (float d)
{
  return 0.2*exp(-0.05*d)-2.*exp(-0.5*d);
}

#define P 0.5
#define SN ivec2(6, 2)

ivec2 N;
ivec2 sN;
int TN;

ivec2
i2xy (ivec3 sid)
{
  return sN*ivec2(sid.x%N.x, sid.x/N.x) + sid.yz;
}

ivec3
xy2i (ivec2 p)
{
  ivec2 pi = p/sN;
  return ivec3(pi.x + pi.y*N.x, p.x%sN.x, p.y%sN.y);
}

ivec2
cross_distribution (int i)
{
  return (1<<(i/4)) * ivec2( ((i&2)/2)^1, (i&2)/2 ) * ( 2*(i%2) - 1 );
}

float
sqr (float x)
{
  return x*x + 1e-2;
}

vec2
hash22 (vec2 p)
{
  vec3 p3 = fract(vec3(p.xyx) * vec3(.1031, .1030, .0973));
  p3 += dot(p3, p3.yzx+33.33);
  return fract((p3.xx+p3.yz)*p3.zy);
}

float
hash13 (vec3 p3)
{
  p3  = fract(p3 * .1031);
  p3 += dot(p3, p3.yzx + 33.33);
  return fract((p3.x + p3.y) * p3.z);
}

vec3
hash32 (vec2 p)
{
  vec3 p3 = fract(vec3(p.xyx) * vec3(.1031, .1030, .0973));
  p3 += dot(p3, p3.yxz+33.33);
  return fract((p3.xxy+p3.yzz)*p3.zyx);
}

vec3
hash33 (vec3 p3)
{
  p3 = fract(p3 * vec3(.1031, .1030, .0973));
  p3 += dot(p3, p3.yxz+33.33);
  return fract((p3.xxy + p3.yxx)*p3.zyx);
}

vec3
hash31 (float p)
{
  vec3 p3 = fract(vec3(p) * vec3(.1031, .1030, .0973));
  p3 += dot(p3, p3.yzx+33.33);
  return fract((p3.xxy+p3.yzz)*p3.zyx); 
}

const float PHI = 0.5*(sqrt(5.) + 1.);

vec2
inverseSF (vec3 p, float n)
{
  float m = 1.0 - 1.0/n;

  float phi = min(atan(p.y, p.x), PI), cosTheta = p.z;

  float k  = max(2.0, floor( log(n * PI * sqrt(5.0) * (1.0 - cosTheta*cosTheta))/ log(PHI+1.0)));
  float Fk = pow(PHI, k)/sqrt(5.0);
  vec2  F  = vec2( round(Fk), round(Fk * PHI) ); // k, k+1

  vec2 ka = 2.0*F/n;
  vec2 kb = 2.0*PI*( fract((F+1.0)*PHI) - (PHI-1.0) );    

  mat2 iB = mat2( ka.y, -ka.x, 
                  kb.y, -kb.x ) / (ka.y*kb.x - ka.x*kb.y);

  vec2 c = floor( iB * vec2(phi, cosTheta - m));
  float d = 8.0;
  float j = 0.0;
  for( int s=0; s<4; s++ ) 
  {
    vec2 uv = vec2( float(s-2*(s/2)), float(s/2) );

    float i = round(dot(F, uv + c));

    float phi = 2.0*PI*fract(i*PHI);
    float cosTheta = m - 2.0*i/n;
    float sinTheta = sqrt(1.0 - cosTheta*cosTheta);

    vec3 q = vec3( cos(phi)*sinTheta, sin(phi)*sinTheta, cosTheta );
    float squaredDistance = dot(q-p, q-p);
    if (squaredDistance < d) 
    {
      d = squaredDistance;
      j = i;
    }
  }

  return vec2( j, sqrt(d) );
}

mat4
getPerspective (float fov, float aspect, float n, float f)
{
  float scale = tan(fov * PI / 360.) * n; 
  float r = aspect * scale, l = -r; 
  float t = scale, b = -t; 

  return mat4(2. * n / (r - l), 0, 0, 0,
              0, 2. * n / (t - b), 0, 0,
              (r + l) / (r - l), (t + b) / (t - b), -(f + n) / (f - n), -1,
              0, 0, -2. * f * n / (f - n), 0);
}

mat4
getRot (vec2 a)
{
  mat4 theta_rot = mat4(1, 0, 0, 0,
                        0, cos(a.y), sin(a.y), 0,
                        0, -sin(a.y), cos(a.y), 0,
                        0, 0, 0, 1); 
        
  mat4 phi_rot = mat4(cos(a.x), sin(a.x), 0, 0,
                      -sin(a.x), cos(a.x), 0, 0,
                      0, 0, 1, 0,
                      0, 0, 0, 1); 

  return transpose(phi_rot*theta_rot);
}

mat4
getModel (vec3 dx)
{
  return transpose(mat4(1, 0, 0, dx.x,
                        0, 1, 0, dx.y,
                        0, 0, 1, dx.z,
                        0, 0, 0, 1));
}
// glut_823_common.glsl

uniform vec2 iResolution;
uniform float iTime;
uniform int iFrame;
uniform vec4 iMouse;
uniform sampler2D iChannel0;
uniform sampler2D iChannel1;

mat4 rotat;
mat4 model;
mat4 imat;

#define range 300.
#define FOV 2.

vec3 ray;
float d;
vec3 ip;
int id;
vec2 p;

float
sphere_intersection (vec3 r, vec3 p, vec4 sphere)
{
  p = p - sphere.xyz;
  if(p == vec3(0))
    return sphere.w;

  float b = dot(p, r);
  float c = sphere.w*sphere.w - dot(p,p);
  float d = b*b + c;

  if((d <= 0.))
    return -length(cross(r, sphere.xyz));

  return -sqrt(d) - b;
}

vec4
ppos (int id)
{
  return vec4(texel(ch0, i2xy(ivec3(id, 0, 0))).xyz,1.);
}

void
getRay ()
{
  vec2 uv = (p - R*0.5)/R.x;
  ray = normalize(vec3(FOV*uv, 1.));
}

float
zrange (float z)
{
  return clamp(z/range,0.,1.);
}

vec4
point_distance (int id, float r)
{
  vec4 X = model*rotat*ppos(id);
  float cd = sphere_intersection(ray, vec3(0), vec4(X.xyz,r));
  if(cd > 0.)
    return vec4((imat*vec4(cd*ray,1.)).xyz, zrange(cd));

  return vec4(vec3(1e10), 1.+abs(cd));
}

void
sort (int utemp)
{
  if(utemp < 0)
    return;
  vec4 dtemp = point_distance(utemp, 0.75);
  if(dtemp.w < d)
  {
    d = dtemp.w;
    ip = dtemp.xyz;
    id = utemp;
  }
}

void
main ()
{
  vec4 Q;
  vec2 pos = gl_FragCoord.xy;

  sN = SN; 
  N = ivec2(R*P/vec2(sN));
  TN = N.x*N.y;
  d = 1e10;
  id = 1;
  p = pos;
  ip = vec3(1e10);
  ivec2 pi = ivec2(floor(pos));

  getRay(); 

  vec2 angles = (iMouse.z>0.)?(iMouse.xy/R)*vec2(2.*PI, PI):vec2(0.3*iTime, PI*0.5+0.5*sin(0.5*iTime));
  rotat = getRot(angles);
  model = getModel(vec3(0.,0.,100.));
  imat = inverse(model*rotat);

  sort(int(texel(ch1, pi).x));

  int ID = id;
  loop(j,16)
  {
    int nbid = int(texel(ch1, pi+cross_distribution(j)).x);
    sort(nbid);
  }

  loop(j,int(sN.x))
  {
    vec4 nb = texel(ch0, i2xy(ivec3(ID, j, 1)));
    loop(i,2)
      sort(int(nb[i]));
  }
    
  loop(i,4)
    sort(int(float(TN)*hash13(vec3(iFrame, pi.x, pi.y*i))));

  Q = vec4(id, ip);

  gl_FragColor = Q;
}
